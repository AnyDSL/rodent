// Vectorizer intrinsics  ----------------------------------------------------------

extern "C" {
    fn "rv_any"     any(bool) -> bool;
    fn "rv_all"     all(bool) -> bool;
    fn "rv_ballot"  ballot(bool) -> int;
    fn "rv_extract" extract(float, int) -> float;
    fn "rv_align"   align(&i8, int)-> &i8;
}

// Misc. ---------------------------------------------------------------------------

// Loads a bounding box ordered by octant on the CPU
fn make_cpu_ordered_bbox(bounds_ptr: &[i8], k: int, octant: int, size: int) -> BBox {
    let (ox, oy, oz) = (octant & 1, (octant >> 1) & 1, (octant >> 2) & 1);
    let (ix, iy, iz) = (ox * size, 2 * size + oy * size, 4 * size + oz * size);

    let (xmin, ymin, zmin) = (
        *(align(&bounds_ptr((ix ^ size) + k), 16) as &float),
        *(align(&bounds_ptr((iy ^ size) + k), 16) as &float),
        *(align(&bounds_ptr((iz ^ size) + k), 16) as &float)
    );
    let (xmax, ymax, zmax) = (
        *(align(&bounds_ptr(ix + k), size) as &float),
        *(align(&bounds_ptr(iy + k), size) as &float),
        *(align(&bounds_ptr(iz + k), size) as &float)
    );

    make_bbox(make_vec3(xmin, ymin, zmin), make_vec3(xmax, ymax, zmax))
}

// Iterate over the bit that are set in a mask
fn one_bits(mask: int, body: fn (int) -> ()) -> () {
    if mask != 0 {
        let lane = cpu_ctz32(mask, true);
        body(lane);
        one_bits(mask & (mask - 1), body);
    }
}

fn extract_ray(ray: Ray, lane: int) -> Ray {
    Ray {
        org: make_vec3(extract(ray.org.x, lane), extract(ray.org.y, lane), extract(ray.org.z, lane)),
        dir: make_vec3(extract(ray.dir.x, lane), extract(ray.dir.y, lane), extract(ray.dir.z, lane)),
        inv_dir: make_vec3(extract(ray.inv_dir.x, lane), extract(ray.inv_dir.y, lane), extract(ray.inv_dir.z, lane)),
        inv_org: make_vec3(extract(ray.inv_org.x, lane), extract(ray.inv_org.y, lane), extract(ray.inv_org.z, lane)),
        tmin: extract(ray.tmin, lane),
        tmax: extract(ray.tmax, lane)
    }
}

// BVH4 ----------------------------------------------------------------------------

struct Bvh4 {
    nodes: &[Bvh4Node],
    tris:  &[Bvh4Tri]
}

struct Bvh4Tri {
    v0: [[float * 4] * 3],
    e1: [[float * 4] * 3],
    e2: [[float * 4] * 3],
    n:  [[float * 4] * 3],
    id:  [int   * 4]
}

struct Bvh4Node {
    bounds: [[float * 4] * 6],
    child:  [int   * 4]
}

fn make_cpu_bvh4(bvh4: Bvh4) -> Bvh {
    Bvh {
        node: |j| {
            let node_ptr = &bvh4.nodes(j);
            BvhNode {
                bbox: |i, octant| if octant >= 0 {
                    make_cpu_ordered_bbox(&node_ptr.bounds as &[i8], i, octant, 16)
                } else {
                    make_bbox(make_vec3(node_ptr.bounds(0)(i), node_ptr.bounds(2)(i), node_ptr.bounds(4)(i)),
                              make_vec3(node_ptr.bounds(1)(i), node_ptr.bounds(3)(i), node_ptr.bounds(5)(i)))
                },
                child: |i| node_ptr.child(i),
                prefetch: || {
                    let ptr = node_ptr as &[u8];
                    cpu_prefetch(&ptr(0),  0 /* read */, 3 /* closest locality */, 1 /* data */);
                    cpu_prefetch(&ptr(64), 0 /* read */, 3 /* closest locality */, 1 /* data */);
                }
            }
        },
        tri: |j| {
            let tri_ptr = &bvh4.tris(j);
            BvhTri {
                load: |i| {
                    make_tri(make_vec3(tri_ptr.v0(0)(i), tri_ptr.v0(1)(i), tri_ptr.v0(2)(i)),
                             make_vec3(tri_ptr.e1(0)(i), tri_ptr.e1(1)(i), tri_ptr.e1(2)(i)),
                             make_vec3(tri_ptr.e2(0)(i), tri_ptr.e2(1)(i), tri_ptr.e2(2)(i)),
                             make_vec3(tri_ptr. n(0)(i), tri_ptr. n(1)(i), tri_ptr. n(2)(i)))
                },
                id: |i| tri_ptr.id(i),
                size: 4
            }
        },
        arity: 4
    }
}

// BVH8 ----------------------------------------------------------------------------

struct Bvh8 {
    nodes: &[Bvh8Node],
    tris:  &[Bvh8Tri]
}

struct Bvh8Tri {
    v0: [[float * 8] * 3],
    e1: [[float * 8] * 3],
    e2: [[float * 8] * 3],
    n:  [[float * 8] * 3],
    id:  [int   * 8]
}

struct Bvh8Node {
    bounds: [[float * 8] * 6],
    child:  [int   * 8]
}

fn make_cpu_bvh8(bvh8: Bvh8) -> Bvh {
    Bvh {
        node: |j| {
            let node_ptr = &bvh8.nodes(j);
            BvhNode {
                bbox: |i, octant| if octant >= 0 {
                    make_cpu_ordered_bbox(&node_ptr.bounds as &[i8], i, octant, 32)
                } else {
                    make_bbox(make_vec3(node_ptr.bounds(0)(i), node_ptr.bounds(2)(i), node_ptr.bounds(4)(i)),
                              make_vec3(node_ptr.bounds(1)(i), node_ptr.bounds(3)(i), node_ptr.bounds(5)(i)))
                },
                child: |i| node_ptr.child(i),
                prefetch: || {
                    let ptr = node_ptr as &[u8];
                    cpu_prefetch(&ptr(0),   0 /* read */, 3 /* closest locality */, 1 /* data */);
                    cpu_prefetch(&ptr(64),  0 /* read */, 3 /* closest locality */, 1 /* data */);
                    cpu_prefetch(&ptr(128), 0 /* read */, 3 /* closest locality */, 1 /* data */);
                    cpu_prefetch(&ptr(192), 0 /* read */, 3 /* closest locality */, 1 /* data */);
                }
            }
        },
        tri: |j| {
            let tri_ptr = &bvh8.tris(j);
            BvhTri {
                load: |i| {
                    make_tri(make_vec3(tri_ptr.v0(0)(i), tri_ptr.v0(1)(i), tri_ptr.v0(2)(i)),
                             make_vec3(tri_ptr.e1(0)(i), tri_ptr.e1(1)(i), tri_ptr.e1(2)(i)),
                             make_vec3(tri_ptr.e2(0)(i), tri_ptr.e2(1)(i), tri_ptr.e2(2)(i)),
                             make_vec3(tri_ptr. n(0)(i), tri_ptr. n(1)(i), tri_ptr. n(2)(i)))
                },
                id: |i| tri_ptr.id(i),
                size: 4
            }
        },
        arity: 8
    }
}

// Ray-box intrinsics  -------------------------------------------------------------

fn make_ray_box_intrinsics_avx() -> RayBoxIntrinsics {
    make_ray_box_intrinsics(cpu_intrinsics.fminf, cpu_intrinsics.fmaxf, |a, b| a <= b)
}

fn make_ray_box_intrinsics_avx2() -> RayBoxIntrinsics {
    // AVX2 intrinsics: use integer min/max instead of floating point min/max
    let fminf = |x, y| {
        let (x_, y_) = (bitcast[int](x), bitcast[int](y));
        bitcast[float](select(x_ < y_, x_, y_))
    };
    let fmaxf = |x, y| {
        let (x_, y_) = (bitcast[int](x), bitcast[int](y));
        bitcast[float](select(x_ > y_, x_, y_))
    };
    make_ray_box_intrinsics(fminf, fmaxf, |a, b| { bitcast[int](a) <= bitcast[int](b) })
}

// Ray layouts  --------------------------------------------------------------------

fn make_cpu_ray1_layout(rays: &[Ray1AoS], hits: &mut [Hit1AoS]) -> RayLayout {
    RayLayout {
        packet_size: 1,
        input: |i, j| {
            let ray = &rays(i + j);
            make_ray(make_vec3(ray.org(0), ray.org(1), ray.org(2)),
                     make_vec3(ray.dir(0), ray.dir(1), ray.dir(2)),
                     ray.tmin, ray.tmax)
        },
        output: |i, j, result| {
            let hit = &mut hits(i + j);
            hit.tri_id = result.prim_id;
            hit.t = result.distance;
            hit.u = result.uv_coords.x;
            hit.v = result.uv_coords.y;
        }
    }
}

fn make_cpu_ray4_layout(rays: &[Ray4SoA], hits: &mut [Hit4SoA]) -> RayLayout {
    RayLayout {
        packet_size: 4,
        input: |i, j| {
            let ray = &rays(i);
            make_ray(make_vec3(ray.org(0)(j), ray.org(1)(j), ray.org(2)(j)),
                     make_vec3(ray.dir(0)(j), ray.dir(1)(j), ray.dir(2)(j)),
                     ray.tmin(j), ray.tmax(j))
        },
        output: |i, j, result| {
            let hit = &mut hits(i);
            hit.tri_id(j) = result.prim_id;
            hit.t(j) = result.distance;
            hit.u(j) = result.uv_coords.x;
            hit.v(j) = result.uv_coords.y;
        }
    }
}

fn make_cpu_ray8_layout(rays: &[Ray8SoA], hits: &mut [Hit8SoA]) -> RayLayout {
    RayLayout {
        packet_size: 8,
        input: |i, j| {
            let ray = &rays(i);
            make_ray(make_vec3(ray.org(0)(j), ray.org(1)(j), ray.org(2)(j)),
                     make_vec3(ray.dir(0)(j), ray.dir(1)(j), ray.dir(2)(j)),
                     ray.tmin(j), ray.tmax(j))
        },
        output: |i, j, result| {
            let hit = &mut hits(i);
            hit.tri_id(j) = result.prim_id;
            hit.t(j) = result.distance;
            hit.u(j) = result.uv_coords.x;
            hit.v(j) = result.uv_coords.y;
        }
    }
}

// Mappings  -----------------------------------------------------------------------

fn make_cpu_packet_mapping(bvh: Bvh, ray_box_intrinsics: RayBoxIntrinsics) -> TraversalMapping {
    TraversalMapping {
        iterate_children: |node_ref, tmax, stack, body, exit| -> ! @{
            stack.pop();

            if all(node_ref.tmin > tmax) { exit() }

            let node = bvh.node(node_ref.node - 1);
            for k in @unroll(0, bvh.arity) @{
                let child_id = node.child(k);
                if child_id == 0 { break() }

                let (hit, tentry, _) = @body(node.bbox(k, -1));

                if any(hit) {
                    if any(stack.top().tmin > tentry) {
                        @stack.push(child_id, tentry);
                    } else {
                        @stack.push_after(child_id, tentry);
                    }
                }
            }
        },
        iterate_primitives: |leaf_ref, tmax, body, exit| -> ! @{
            if all(leaf_ref.tmin > tmax) { exit() }

            let mut tri_id = !leaf_ref.node;
            while true @{
                let tri = bvh.tri(tri_id++);
                for k in @unroll(0, tri.size) @{
                    body(make_ray_tri_intersector(tri.load(k), tri.id(k) & 0x7FFFFFFF));
                }
                if (tri.id(3) & bitcast[i32](0x80000000u)) != 0 { break() }
            }
        },
        enter_sublevel: no_sublevel,
        intrinsics: cpu_intrinsics,
        ray_box_intrinsics: ray_box_intrinsics,
        ray_box_ordered: false
    }
}

fn make_cpu_single_mapping(k: int, octant: int, bvh: Bvh, ray_box_intrinsics: RayBoxIntrinsics) -> TraversalMapping {
    TraversalMapping {
        iterate_children: |node_ref, tmax, stack, body, exit| -> ! @{
            stack.pop();
            if unlikely(node_ref.tmin > tmax) { exit() }

            let node = bvh.node(node_ref.node - 1);
            let (hit, tentry, _) = @body(node.bbox(k, octant));

            // Push intersected nodes on the stack
            let mask = ballot(hit);
            if likely(mask == 0) { exit() }

            let mut count = 0;
            for lane in one_bits(mask) @{
                let child_id = node.child(lane);
                if child_id == 0 { break() }

                count++;
                bvh.node(child_id - 1).prefetch();

                // Process the closest child first
                let t = extract(tentry, lane);
                if stack.top().tmin > t {
                    @stack.push(child_id, t);
                } else {
                    @stack.push_after(child_id, t);
                }
            }

            if likely(count <= 2) { exit() }
            if likely(count == 3) { @stack.sort_n(2) }
            else                  { @stack.sort_n(3) }
        },
        iterate_primitives: |leaf_ref, tmax, body, exit| -> ! @{
            if unlikely(leaf_ref.tmin > tmax) { exit() }

            let mut tri_id = !leaf_ref.node;
            while true @{
                let tri = bvh.tri(tri_id++);

                // This assumes that the number of triangles per packet is a
                // multiple of the BVH arity (which is also the vector width)
                for i in @unroll(0, tri.size / bvh.arity) {
                    let closest_intr : Intersector = |math, ray, intr| @{
                        // Compute the intersection for each lane
                        let (found, t, u, v) = @intersect_ray_tri(math, ray, tri.load(i + k));

                        // Find the closest intersection
                        let mask = ballot(found);
                        if mask != 0 {
                            let mut closest_hit = @make_hit(-1, -1, flt_max, undef());
                            for lane in one_bits(mask) @{
                                let tlane = extract(t, lane);
                                if tlane < closest_hit.distance @{
                                    closest_hit.prim_id = tri.id(i + lane) & 0x7FFFFFFF;
                                    closest_hit.uv_coords = make_vec2(extract(u, lane), extract(v, lane));
                                    closest_hit.distance = tlane;
                                }
                            }
                            @intr(closest_hit)
                        }
                    };

                    @body(closest_intr);
                }

                if (tri.id(tri.size - 1) & bitcast[i32](0x80000000u)) != 0 { break() }
            }
        },
        enter_sublevel: no_sublevel,
        intrinsics: cpu_intrinsics,
        ray_box_intrinsics: ray_box_intrinsics,
        ray_box_ordered: true
    }
}

fn make_cpu_hybrid_mapping(config: TraversalConfig, bvh: Bvh, ray_box_intrinsics: RayBoxIntrinsics, vector_width: int) -> TraversalMapping @{
    let packet_mapping   = make_cpu_packet_mapping(bvh, ray_box_intrinsics);
    let switch_threshold = match vector_width {
        4  =>  3,
        8  =>  5,
        16 => 14,
        _  => vector_width // Be conservative with unknown SIMD widths
    };

    TraversalMapping {
        enter_sublevel: |stack, ray, body| {
            let level_ref = stack.top();
            let active = level_ref.tmin <= ray.tmax;
            if cpu_popcount32(ballot(active)) < switch_threshold @{
                stack.pop();

                // Switch to single ray traversal when the SIMD utilization is too low
                for lane in @unroll(0, vector_width) @{
                    pe_info("single ray lane", lane);
                    let hit = cpu_traverse_single_helper(config, bvh, @extract_ray(ray, lane), ray_box_intrinsics);
                    if hit.prim_id >= 0 { @body(hit) }
                }
            }
        },
        iterate_children: packet_mapping.iterate_children,
        iterate_primitives: packet_mapping.iterate_primitives,
        intrinsics: cpu_intrinsics,
        ray_box_intrinsics: ray_box_intrinsics,
        ray_box_ordered: true
    }
}

// Variants  -----------------------------------------------------------------------

fn cpu_traverse_single_helper(config: TraversalConfig, bvh: Bvh, ray: Ray, ray_box_intrinsics: RayBoxIntrinsics) -> Hit @{
    let mut hit : Hit;
    pe_info("arity", bvh.arity);
    for k in vectorize(bvh.arity, bvh.arity * sizeof[float](), 0, bvh.arity) @{
        let stack = allocate_stack();

        stack.push(1, ray.tmin);
        let mapping = @make_cpu_single_mapping(k, ray_octant(ray), bvh, ray_box_intrinsics);
        hit = @traverse_bvh(mapping, config, stack, ray)
    }
    hit
}

fn cpu_traverse_packet_avx2(config: TraversalConfig, bvh: Bvh, ray_layout: RayLayout, ray_count: int) -> () {
    let inc = ray_layout.packet_size;
    for i in $range(0, ray_count / inc) {
        for j in vectorize(inc, 32, 0, inc) @{
            let ray = ray_layout.input(i, j);
            let stack = allocate_stack();

            stack.push(1, ray.tmin);

            let mapping = @make_cpu_packet_mapping(bvh, make_ray_box_intrinsics_avx2());
            @ray_layout.output(i, j, @traverse_bvh(mapping, config, stack, ray));
        }
    }
}

fn cpu_traverse_hybrid_avx2(config: TraversalConfig, bvh: Bvh, ray_layout: RayLayout, ray_count: int) -> () {
    let inc = ray_layout.packet_size;
    for i in $range(0, ray_count / inc) {
        for j in vectorize(inc, 32, 0, inc) @{
            let ray = ray_layout.input(i, j);
            let stack = allocate_stack();

            stack.push(1, ray.tmin);

            let mapping = @make_cpu_hybrid_mapping(config, bvh, make_ray_box_intrinsics_avx2(), inc);
            @ray_layout.output(i, j, @traverse_bvh(mapping, config, stack, ray));
        }
    }
}

fn cpu_traverse_single_avx2(config: TraversalConfig, bvh: Bvh, ray_layout: RayLayout, ray_count: int) -> () {
    let inc = ray_layout.packet_size;
    for i in $range(0, ray_count) @{
        let (packet_id, ray_id) = (i / inc, i % inc);
        let ray = ray_layout.input(packet_id, ray_id);
        cpu_traverse_single_helper(config, bvh, ray, make_ray_box_intrinsics_avx2());
    }
}
