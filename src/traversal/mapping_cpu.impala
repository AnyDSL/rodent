struct Bvh4 {
    nodes: &[Bvh4Node],
    tris:  &[Bvh4Tri]
}

struct Bvh4Tri {
    v0: [[float * 4] * 3],
    e1: [[float * 4] * 3],
    e2: [[float * 4] * 3],
    n:  [[float * 4] * 3],
    id:  [int   * 4]
}

struct Bvh4Node {
    min:   [[float * 4] * 3],
    max:   [[float * 4] * 3],
    child:  [int   * 4]
}

extern "C" {
    fn "rv_any"    any(bool) -> bool;
    fn "rv_all"    all(bool) -> bool;
    fn "rv_ballot" ballot(bool) -> int;
}

fn cpu_traverse_bvh_packet(bvh: Bvh4, rays: RayStream, hits: HitStream, ray_count: int) -> () {
    for i in range_step(0, ray_count, 8) {
        for j in vectorize(8, 32, 0, 8) {
            let (ray, tmin, tmax) = rays(i + j);
            let hit = hits(i + j);
            let stack = alloc_stack();

            stack.push(NodeId{ id: 0 }, tmin);

            let mapping = TraversalMapping {
                iterate_children: |node_id, tmax, stack, body, exit| -> ! {
                    let tmin = stack.tmin();
                    stack.pop();

                    if tmin > tmax { exit() }

                    let node = bvh.nodes(node_id.id);
                    for k in range(0, 4) {
                        let bbox = bbox(vec3(node.min(0)(k), node.min(1)(k), node.min(2)(k)),
                                        vec3(node.max(0)(k), node.max(1)(k), node.max(2)(k)));

                        body(bbox, |tentry, texit| {
                            if any(true) {
                                if any(stack.tmin() > tentry) @{
                                    stack.push(NodeId { id: node.child(k) }, tentry);
                                } else @{
                                    stack.insert(NodeId { id: node.child(k) }, tentry);
                                }
                            }
                        });
                    }
                },
                iterate_triangles: |leaf_id, tmax, stack, body, exit| -> ! {
                    if stack.tmin() > tmax { exit() }

                    let mut tri_id = !stack.top().id;
                    while true {
                        let t = bvh.tris(tri_id++);
                        for k in range(0, 4) {
                            @body(tri(vec3(t.v0(0)(k), t.v0(1)(k), t.v0(2)(k)),
                                      vec3(t.e1(0)(k), t.e1(1)(k), t.e1(2)(k)),
                                      vec3(t.e2(0)(k), t.e2(1)(k), t.e2(2)(k)),
                                      vec3(t. n(0)(k), t. n(1)(k), t. n(2)(k))), t.id(k));
                        }
                        if t.id(0) < 0 { break() }
                    }
                },
                is_leaf: |node_id| node_id.id < 0
            };

            traverse_bvh(mapping, stack, ray, hit, tmin, tmax, false);
        }
    }
}

fn cpu_traverse_bvh_single(bvh: Bvh4, rays: RayStream, hits: HitStream, ray_count: int) -> () {
    for i in range(0, ray_count) {
        let (ray, tmin, tmax) = rays(i);
        let hit = hits(i);

        for j in vectorize(4, 16, 0, 4) {
            let stack = alloc_stack();

            stack.push(NodeId{ id: 0 }, tmin);

            let mapping = TraversalMapping {
                iterate_children: |node_id, tmax, stack, body, exit| -> ! {
                    let tmin = stack.tmin();
                    stack.pop();

                    if tmin > tmax { exit() }

                    let node = bvh.nodes(node_id.id);
                    let bbox = bbox(vec3(node.min(0)(j), node.min(1)(j), node.min(2)(j)),
                                    vec3(node.max(0)(j), node.max(1)(j), node.max(2)(j)));

                    let mut child : [int   * 4] = [-1, -1, -1, -1];
                    let mut entry : [float * 4];
                    body(bbox, |tentry, texit| {
                        entry(j) = tentry;
                        child(j) = node.child(j);
                    });

                    let mut count = 0;
                    for k in range(0, 4) {
                        if child(k) >= 0 {
                            stack.push(NodeId{ id: child(k) }, entry(k));
                            count++;
                        }
                    }

                    if count < 2 { exit() }
                    if count == 2 {
                        @stack.sort_n(2);
                    } else if count == 3 {
                        @stack.sort_n(3);
                    } else if count == 4 {
                        @stack.sort_n(4);
                    }
                },
                iterate_triangles: |leaf_id, tmax, stack, body, exit| -> ! {
                    if stack.tmin() > tmax { exit() }

                    let mut tri_id = !stack.top().id;
                    while true {
                        let t = bvh.tris(tri_id++);
                        @body(tri(vec3(t.v0(0)(j), t.v0(1)(j), t.v0(2)(j)),
                                  vec3(t.e1(0)(j), t.e1(1)(j), t.e1(2)(j)),
                                  vec3(t.e2(0)(j), t.e2(1)(j), t.e2(2)(j)),
                                  vec3(t. n(0)(j), t. n(1)(j), t. n(2)(j))), t.id(j));
                        if t.id(0) < 0 { break() }
                    }
                },
                is_leaf: |node_id| node_id.id < 0
            };

            traverse_bvh(mapping, stack, ray, hit, tmin, tmax, false);
        }
    }
}
