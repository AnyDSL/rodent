// Driver functions ----------------------------------------------------------------

extern "C" {
    fn rodent_cpu_get_bvh8_tri4(&mut Bvh8Tri4) -> ();
    fn rodent_cpu_get_film_data(&mut PixelData) -> ();
    fn rodent_cpu_load_tri_mesh(&[u8], &mut TriMesh) -> ();
    fn rodent_cpu_load_pixel_data(&[u8], &mut PixelData) -> ();
    fn rodent_cpu_get_primary_stream(&mut PrimaryStream, i32) -> ();
    fn rodent_cpu_get_secondary_stream(&mut SecondaryStream, i32) -> ();
}

// Ray streams ---------------------------------------------------------------------

struct RayStream {
    id: &mut [i32], // this field is also used to indicate if the ray is alive
    org_x: &mut [f32],
    org_y: &mut [f32],
    org_z: &mut [f32],
    dir_x: &mut [f32],
    dir_y: &mut [f32],
    dir_z: &mut [f32],
    tmin: &mut [f32],
    tmax: &mut [f32],
}

struct PrimaryStream {
    size: i32,
    rays: RayStream,
    shader_id: &mut [i32],
    geom_id: &mut [i32],
    prim_id: &mut [i32],
    t: &mut [f32],
    u: &mut [f32],
    v: &mut [f32],
    rnd: &mut [RndState],
    mis: &mut [f32],
    contrib_r: &mut [f32],
    contrib_g: &mut [f32],
    contrib_b: &mut [f32],
    depth: &mut [i32]
}

struct SecondaryStream {
    size: i32,
    rays: RayStream,
    prim_id: &mut [i32],
    color_r: &mut [f32],
    color_g: &mut [f32],
    color_b: &mut [f32]
}

fn @cpu_get_primary_state(primary: PrimaryStream, i: i32) -> RayState {
    RayState {
        rnd:     primary.rnd(i),
        contrib: make_color(primary.contrib_r(i), primary.contrib_g(i), primary.contrib_b(i)),
        mis:     primary.mis(i),
        depth:   primary.depth(i)
    }
}

fn @cpu_set_primary_state(primary: PrimaryStream, i: i32, state: RayState) -> () {
    primary.rnd(i)       = state.rnd;
    primary.contrib_r(i) = state.contrib.r;
    primary.contrib_g(i) = state.contrib.g;
    primary.contrib_b(i) = state.contrib.b;
    primary.mis(i)       = state.mis;
    primary.depth(i)     = state.depth;
}

fn @make_cpu_ray_accessors(rays: RayStream, vector_width: i32) -> (fn (i32, i32) -> Ray, fn (i32, i32, Ray) -> ()) {
    let read_ray = @ |i, j| {
        let k = i * vector_width + j;
        make_ray(
            make_vec3(rays.org_x(k),
                      rays.org_y(k),
                      rays.org_z(k)),
            make_vec3(rays.dir_x(k),
                      rays.dir_y(k),
                      rays.dir_z(k)),
            rays.tmin(k),
            rays.tmax(k)
        )
    };
    let write_ray = @ |i, j, ray| {
        let k = i * vector_width + j;
        rays.org_x(k) = ray.org.x;
        rays.org_y(k) = ray.org.y;
        rays.org_z(k) = ray.org.z;
        rays.dir_x(k) = ray.dir.x;
        rays.dir_y(k) = ray.dir.y;
        rays.dir_z(k) = ray.dir.z;
        rays.tmin(k)  = ray.tmin;
        rays.tmax(k)  = ray.tmax;
    };
    (read_ray, write_ray)
}

fn @make_cpu_primary_ray_layout(primary: PrimaryStream, vector_width: i32) -> RayLayout {
    let (read_ray, write_ray) = make_cpu_ray_accessors(primary.rays, vector_width);
    RayLayout {
        packet_size: vector_width,
        read_ray: read_ray,
        write_ray: write_ray,
        read_hit: @ |i, j| {
            let k = i * vector_width + j;
            make_hit(
                primary.geom_id(k),
                primary.prim_id(k),
                primary.t(k),
                make_vec2(primary.u(k),
                          primary.v(k))
            )
        },
        write_hit: @ |i, j, hit| {
            let k = i * vector_width + j;
            primary.geom_id(k) = hit.geom_id;
            primary.prim_id(k) = hit.prim_id;
            primary.t(k)       = hit.distance;
            primary.u(k)       = hit.uv_coords.x;
            primary.v(k)       = hit.uv_coords.y;
        }
    }
}

fn @make_cpu_secondary_ray_layout(secondary: SecondaryStream, vector_width: i32) -> RayLayout {
    let (read_ray, write_ray) = make_cpu_ray_accessors(secondary.rays, vector_width);
    RayLayout {
        packet_size: vector_width,
        read_ray: read_ray,
        write_ray: write_ray,
        read_hit: @ |i, j| {
            let k = i * vector_width + j;
            make_hit(-1, secondary.prim_id(k), 0.0f, make_vec2(0.0f, 0.0f))
        },
        write_hit: @ |i, j, hit| {
            let k = i * vector_width + j;
            secondary.prim_id(k) = hit.prim_id;
        }
    }
}

// Trace function ------------------------------------------------------------------

fn @cpu_parallel_tiles( width: i32
                      , height: i32
                      , tile_width: i32
                      , tile_height: i32
                      , num_cores: i32
                      , body: fn (i32, i32, i32, i32) -> ()) -> () {
    let num_tiles_x = round_up(width , tile_width)  / tile_width;
    let num_tiles_y = round_up(height, tile_height) / tile_height;
    let num_tiles = num_tiles_x * num_tiles_y;
    let tiles_div = make_fast_div(num_tiles_x as u32);

    for i in parallel(num_cores, 0, num_tiles) {
        let y = fast_div(tiles_div, i as u32) as i32;
        let x = i - num_tiles_x * y;
        let xmin = x * tile_width;
        let ymin = y * tile_height;
        let xmax = cpu_intrinsics.min(xmin + tile_width,  width);
        let ymax = cpu_intrinsics.min(ymin + tile_height, height);
        @@body(xmin, ymin, xmax, ymax);
    }
}

fn @cpu_sort_primary(scene: Scene, primary: PrimaryStream, ray_begins: &mut [i32], ray_ends: &mut[i32], vector_width: i32) -> i32 {
    let primary_layout_as_ray1 = make_cpu_primary_ray_layout(primary, 1);

    // Compute shader ids
    for i in vectorized_range(vector_width, 0, primary.size) {
        let mut shader_id = scene.num_shaders;
        let hit = primary_layout_as_ray1.read_hit(i, 0);
        if hit.prim_id/*hit.geom_id*/ >= 0 {
            shader_id = scene.geometries(0/*hit.geom_id*/).shader_id(hit);
        }
        primary.shader_id(i) = shader_id;
    }

    // Count the number of rays per shader
    for i in range(0, scene.num_shaders + 1) {
        ray_ends(i) = 0;
    }
    for i in range(0, primary.size) {
        ray_ends(primary.shader_id(i))++;
    }
    // Compute scan over shader bins
    let mut n = 0;
    for i in range(0, scene.num_shaders + 1) {
        ray_begins(i) = n;
        n += ray_ends(i);
        ray_ends(i) = n;
    }

    // Sort by shader
    for i in range(0, scene.num_shaders) {
        let (begin, end) = (ray_begins(i), ray_ends(i));
        let mut j = begin;
        while j < end {
            let shader_id = primary.shader_id(j);
            if shader_id != i {
                let k = ray_begins(shader_id)++;

                swap_i32(&mut primary.rays.id(k),    &mut primary.rays.id(j));
                swap_f32(&mut primary.rays.org_x(k), &mut primary.rays.org_x(j));
                swap_f32(&mut primary.rays.org_y(k), &mut primary.rays.org_y(j));
                swap_f32(&mut primary.rays.org_z(k), &mut primary.rays.org_z(j));
                swap_f32(&mut primary.rays.dir_x(k), &mut primary.rays.dir_x(j));
                swap_f32(&mut primary.rays.dir_y(k), &mut primary.rays.dir_y(j));
                swap_f32(&mut primary.rays.dir_z(k), &mut primary.rays.dir_z(j));
                swap_f32(&mut primary.rays.tmin(k),  &mut primary.rays.tmin(j));
                swap_f32(&mut primary.rays.tmax(k),  &mut primary.rays.tmax(j));

                swap_i32(&mut primary.shader_id(k), &mut primary.shader_id(j));
                swap_i32(&mut primary.geom_id(k),   &mut primary.geom_id(j));
                swap_i32(&mut primary.prim_id(k),   &mut primary.prim_id(j));
                swap_f32(&mut primary.t(k),         &mut primary.t(j));
                swap_f32(&mut primary.u(k),         &mut primary.u(j));
                swap_f32(&mut primary.v(k),         &mut primary.v(j));
                swap_u32(&mut primary.rnd(k),       &mut primary.rnd(j));
                swap_f32(&mut primary.mis(k),       &mut primary.mis(j));
                swap_f32(&mut primary.contrib_r(k), &mut primary.contrib_r(j));
                swap_f32(&mut primary.contrib_g(k), &mut primary.contrib_g(j));
                swap_f32(&mut primary.contrib_b(k), &mut primary.contrib_b(j));
                swap_i32(&mut primary.depth(k),     &mut primary.depth(j));
            } else {
                j++;
            }
        }
    }

    // Kill rays that have not intersected anything
    let last = ray_ends(scene.num_shaders - 1);
    for i in range(last, primary.size) {
        primary.rays.id(i) = -1;
    }

    last
}

fn @cpu_permute_ray_stream(rays: RayStream, i: i32, j: i32, perm: Perm) -> () {
    rays.org_x(i) = cpu_permute_f32(rays.org_x(j), perm);
    rays.org_y(i) = cpu_permute_f32(rays.org_y(j), perm);
    rays.org_z(i) = cpu_permute_f32(rays.org_z(j), perm);
    rays.dir_x(i) = cpu_permute_f32(rays.dir_x(j), perm);
    rays.dir_y(i) = cpu_permute_f32(rays.dir_y(j), perm);
    rays.dir_z(i) = cpu_permute_f32(rays.dir_z(j), perm);
    rays.tmin(i)  = cpu_permute_f32(rays.tmin(j), perm);
    rays.tmax(i)  = cpu_permute_f32(rays.tmax(j), perm);
}

fn @cpu_compact_primary(primary: PrimaryStream) -> i32 {
    let lut = generate_compaction_lut();
    let mut k = 0;
    for i in range_step(0, primary.size, 8) {
        let d = primary.size - i;
        let valid_mask = if d < 8 { (1 << d) - 1 } else { -1 };
        for j in vectorize(8, sizeof[float](), 0, 8) {
            let id = primary.rays.id(i + j);
            let mask = rv_ballot(id >= 0) & valid_mask;
            let perm = lut(mask);

            primary.rays.id(k + j) = cpu_permute_i32(id, perm);

            cpu_permute_ray_stream(primary.rays, k + j, i + j, perm);

            primary.shader_id(k + j) = cpu_permute_i32(primary.shader_id(i + j), perm);
            primary.geom_id(k + j)   = cpu_permute_i32(primary.geom_id(i + j), perm);
            primary.prim_id(k + j)   = cpu_permute_i32(primary.prim_id(i + j), perm);
            primary.t(k + j)         = cpu_permute_f32(primary.t(i + j), perm);
            primary.u(k + j)         = cpu_permute_f32(primary.u(i + j), perm);
            primary.v(k + j)         = cpu_permute_f32(primary.v(i + j), perm);
            primary.rnd(k + j)       = cpu_permute_u32(primary.rnd(i + j), perm);
            primary.mis(k + j)       = cpu_permute_f32(primary.mis(i + j), perm);
            primary.contrib_r(k + j) = cpu_permute_f32(primary.contrib_r(i + j), perm);
            primary.contrib_g(k + j) = cpu_permute_f32(primary.contrib_g(i + j), perm);
            primary.contrib_b(k + j) = cpu_permute_f32(primary.contrib_b(i + j), perm);
            primary.depth(k + j)     = cpu_permute_i32(primary.depth(i + j), perm);

            k += cpu_popcount32(mask);
        }
    }
    k
}

fn @cpu_compact_secondary(secondary: SecondaryStream) -> i32 {
    let lut = generate_compaction_lut();
    let mut k = 0;
    for i in range_step(0, secondary.size, 8) {
        let d = secondary.size - i;
        let valid_mask = if d < 8 { (1 << d) - 1 } else { -1 };
        for j in vectorize(8, sizeof[float](), 0, 8) {
            let id = secondary.rays.id(i + j);
            let mask = rv_ballot(id >= 0) & valid_mask;
            let perm = lut(mask);

            secondary.rays.id(k + j) = cpu_permute_i32(id, perm);

            cpu_permute_ray_stream(secondary.rays, k + j, i + j, perm);

            secondary.prim_id(k + j) = cpu_permute_i32(secondary.prim_id(i + j), perm);
            secondary.color_r(k + j) = cpu_permute_f32(secondary.color_r(i + j), perm);
            secondary.color_g(k + j) = cpu_permute_f32(secondary.color_g(i + j), perm);
            secondary.color_b(k + j) = cpu_permute_f32(secondary.color_b(i + j), perm);

            k += cpu_popcount32(mask);
        }
    }
    k
}

fn @cpu_generate_rays( primary: PrimaryStream
                     , capacity: i32
                     , path_tracer: PathTracer
                     , id: &mut i32
                     , xmin: i32
                     , ymin: i32
                     , tile_size: i32
                     , film_width: i32
                     , film_height: i32
                     , vector_width: i32
                     ) -> i32 {
    let primary_layout_as_ray1 = make_cpu_primary_ray_layout(primary, 1);
    let first_id = *id;
    let num_rays = cpu_intrinsics.min(tile_size * tile_size - first_id, capacity - primary.size);
    let tile_div = make_fast_div(tile_size as u32);
    for i in vectorized_range(vector_width, 0, num_rays) {
        let in_tile_id = first_id + i;

        // Compute x, y of ray
        let in_tile_y = fast_div(tile_div, in_tile_id as u32) as i32;
        let in_tile_x = in_tile_id - in_tile_y * tile_size;
        let x = xmin + in_tile_x;
        let y = ymin + in_tile_y;
        let (ray, state) = @@(path_tracer.on_emit)(x, y, film_width, film_height);
        let cur_ray = primary.size + i;
        primary_layout_as_ray1.write_ray(cur_ray, 0, ray);
        cpu_set_primary_state(primary, cur_ray, state);
        primary.rays.id(cur_ray) = y * film_width + x;
    }
    *id = first_id + num_rays;
    primary.size + num_rays
}

fn @cpu_trace( scene: Scene
             , path_tracer: PathTracer
             , min_max: MinMax
             , single: bool
             , tile_size: i32
             , num_cores: i32
             , vector_width: i32
             ) -> () {
    let mut film_data : PixelData;
    let mut bvh8tri4  : Bvh8Tri4;
    rodent_cpu_get_film_data(&mut film_data);
    rodent_cpu_get_bvh8_tri4(&mut bvh8tri4);

    let bvh = make_cpu_bvh8_tri4(bvh8tri4);
    for xmin, ymin, xmax, ymax in cpu_parallel_tiles(film_data.width, film_data.height, tile_size, tile_size, num_cores) {
        // Get ray streams/states from the CPU driver
        let mut primary   : PrimaryStream;
        let mut secondary : SecondaryStream;
        let capacity = 1024;
        rodent_cpu_get_primary_stream(&mut primary,     capacity);
        rodent_cpu_get_secondary_stream(&mut secondary, capacity);

        let mut id = 0;
        let num_rays = tile_size * tile_size;
        while id < num_rays || primary.size > 0 {
            // (Re-)generate primary rays
            if primary.size < capacity {
                primary.size = cpu_generate_rays(primary, capacity, path_tracer, &mut id, xmin, ymin, tile_size, film_data.width, film_data.height, vector_width);
            }

            // Trace primary rays
            cpu_traverse_hybrid(
                min_max,
                make_cpu_primary_ray_layout(primary, vector_width),
                bvh,
                single,
                false,
                round_up(primary.size, vector_width),
                1);

            // Sort hits by shader id, and filter invalid hits
            let mut ray_begins : [i32 * 1024];
            let mut ray_ends   : [i32 * 1024];
            primary.size = cpu_sort_primary(scene, primary, &mut ray_begins, &mut ray_ends, vector_width);

            // Perform (vectorized) shading
            let mut begin = 0;
            for shader_id in unroll(0, scene.num_shaders) {
                let end = ray_ends(shader_id);
                for i in vectorized_range(vector_width, begin, end) {
                    let primary_layout_as_ray1   = make_cpu_primary_ray_layout(primary,     1);
                    let secondary_layout_as_ray1 = make_cpu_secondary_ray_layout(secondary, 1);
                    let ray = primary_layout_as_ray1.read_ray(i, 0);
                    let hit = primary_layout_as_ray1.read_hit(i, 0);
                    let mut state = cpu_get_primary_state(primary, i);
                    let ray_id = primary.rays.id(i);

                    let geom   = scene.geometries(hit.geom_id);
                    let surf   = geom.surface_element(cpu_intrinsics, ray, hit);
                    let shader = scene.shaders(shader_id);
                    let mat    = shader(cpu_intrinsics, scene, surf);

                    // Execute hit point shading, and add the contribution of each lane to the frame buffer
                    let mut hit_color = make_color(0.0f, 0.0f, 0.0f);
                    let mut add_color = false;
                    for once() {
                        @@(path_tracer.on_hit)(ray, hit, &mut state, surf, mat, @ |color| -> ! {
                            add_color = true;
                            hit_color = color;
                            break()
                        });
                    }
                    for lane in cpu_one_bits(rv_ballot(add_color)) {
                        let j = bitcast[i32](rv_extract(bitcast[f32](ray_id), lane));
                        film_data.pixels(j).r += rv_extract(hit_color.r, lane);
                        film_data.pixels(j).g += rv_extract(hit_color.g, lane);
                        film_data.pixels(j).b += rv_extract(hit_color.b, lane);
                    }

                    // Compute shadow rays
                    for once() {
                        @@(path_tracer.on_shadow)(ray, hit, &mut state, surf, mat, @ |ray, color| -> ! {
                            secondary_layout_as_ray1.write_ray(i, 0, ray);
                            secondary.color_r(i) = color.r;
                            secondary.color_g(i) = color.g;
                            secondary.color_b(i) = color.b;
                            secondary.rays.id(i) = ray_id;
                            break()
                        }, @ || -> ! {
                            secondary.rays.id(i) = -1;
                            break()
                        })
                    }

                    // Sample new rays
                    for once() {
                        @@(path_tracer.on_bounce)(ray, hit, &mut state, surf, mat, @ |new_ray, new_state| -> ! {
                            primary_layout_as_ray1.write_ray(i, 0, new_ray);
                            cpu_set_primary_state(primary, i, new_state);
                            break()
                        }, @ || -> ! {
                            primary.rays.id(i) = -1;
                            break()
                        })
                    }
                }
                begin = end;
            }

            // Filter terminated rays
            secondary.size = primary.size;
            primary.size   = cpu_compact_primary(primary);
  
            // Compact and trace secondary rays
            secondary.size = cpu_compact_secondary(secondary);
            if likely(secondary.size > 0) {
                cpu_traverse_hybrid(
                    min_max,
                    make_cpu_secondary_ray_layout(secondary, vector_width),
                    bvh,
                    single,
                    true,
                    round_up(secondary.size, vector_width),
                    1);
            }

            // Add the contribution for secondary rays to the frame buffer
            for i in range(0, secondary.size) {
                if secondary.prim_id(i) < 0 {
                    let j = secondary.rays.id(i);
                    film_data.pixels(j).r += secondary.color_r(i);
                    film_data.pixels(j).g += secondary.color_g(i);
                    film_data.pixels(j).b += secondary.color_b(i);
                }
            }
        }
    }
}

fn @make_cpu_mesh_loader() -> fn (&[u8]) -> Geometry {
    @ |file_name| {
        let mut tri_mesh;
        rodent_cpu_load_tri_mesh(file_name, &mut tri_mesh);
        make_tri_mesh_geometry(tri_mesh)
    }
}

fn @make_cpu_image_loader() -> fn (&[u8]) -> Image {
    @ |file_name| {
        let mut pixel_data;
        rodent_cpu_load_pixel_data(file_name, &mut pixel_data);
        make_image(pixel_data)
    }
}

fn @make_cpu_device() -> Device {
    Device {
        intrinsics: cpu_intrinsics,
        trace:      @ |scene, path_tracer| {
            cpu_trace(
                scene,
                path_tracer,
                make_cpu_int_min_max(),
                /* single */    true,
                /* tile_size */   64,
                /* num_cores */    0,
                /* vector_width */ 8
            )
        },
        load_mesh:  make_cpu_mesh_loader(),
        load_image: make_cpu_image_loader()
    }
}
