// Trace function ------------------------------------------------------------------

struct Atomics {
    add_global_i32: fn (&mut i32, i32) -> i32,
    add_global_f32: fn (&mut f32, f32) -> f32,
    add_shared_i32: fn (&mut [3]i32, i32) -> i32,
    add_shared_f32: fn (&mut [3]f32, f32) -> f32
}

fn @gpu_get_film_data(dev_id: i32) -> (&mut [f32], i32, i32) {
    let mut film_pixels : &mut [f32];
    let mut film_width  : i32;
    let mut film_height : i32;
    rodent_get_film_data(dev_id, &mut film_pixels, &mut film_width, &mut film_height);
    (film_pixels, film_width, film_height)
}

fn @gpu_traverse_primary(primary: PrimaryStream, acc: Accelerator, intrinsics: Intrinsics, min_max: MinMax, scene: Scene) -> () {
    gpu_traverse_single(
        acc,
        intrinsics,
        min_max,
        scene.bvh,
        make_ray_stream_reader(primary.rays, 1),
        make_primary_stream_hit_writer(primary, 1, scene.num_geometries),
        1 /*packet_size*/,
        primary.size /*num_packets*/,
        false /*any_hit*/
    );
}

fn @gpu_accumulate(atomics: Atomics, film_pixels: &mut [f32], pixel: i32, color: Color, spp: i32) -> () {
    if spp == 1 {
        // If there is only one sample in flight, there is no need for atomics
        film_pixels(pixel * 3 + 0) += color.r;
        film_pixels(pixel * 3 + 1) += color.g;
        film_pixels(pixel * 3 + 2) += color.b;
    } else {
        atomics.add_global_f32(&mut film_pixels(pixel * 3 + 0), color.r * (1.0f / (spp as f32)));
        atomics.add_global_f32(&mut film_pixels(pixel * 3 + 1), color.g * (1.0f / (spp as f32)));
        atomics.add_global_f32(&mut film_pixels(pixel * 3 + 2), color.b * (1.0f / (spp as f32)));
    }
}

fn @gpu_traverse_secondary(secondary: SecondaryStream, acc: Accelerator, intrinsics: Intrinsics, atomics: Atomics, min_max: MinMax, scene: Scene, film_pixels: &mut [f32], spp: i32) -> () {
    let block_w = 64;
    let grid  = (round_up(secondary.size, block_w), 1, 1);
    let block = (block_w, 1, 1);
    with work_item in acc.exec(grid, block) {
        let gid = work_item.gidx();
        if gid >= secondary.size {
            break()
        }

        let pixel = secondary.rays.id(gid);
        if pixel < 0 {
            break()
        }

        let ray = make_ray_stream_reader(secondary.rays, 1)(gid, 0);
        let hit = gpu_traverse_single_helper(
            intrinsics,
            min_max,
            ray,
            scene.bvh,
            true /*any_hit*/,
            1 /*root*/
        );
        if hit.geom_id < 0 {
            let color = make_color(
                secondary.color_r(gid),
                secondary.color_g(gid),
                secondary.color_b(gid)
            );
            gpu_accumulate(atomics, film_pixels, pixel, color, spp);
        }
    }
}

fn @gpu_shade(acc: Accelerator, atomics: Atomics, scene: Scene, path_tracer: PathTracer, film_pixels: &mut [f32], spp: i32, primary: PrimaryStream, secondary: SecondaryStream, first: i32, last: i32, geom_id: i32) -> () {
    let n = last - first;
    let block_w = 64;
    let grid  = (round_up(n, block_w), 1, 1);
    let block = (block_w, 1, 1);
    with work_item in acc.exec(grid, block) {
        let ray_id = first + work_item.gidx();
        if ray_id >= last {
            break()
        }

        let mut state = make_primary_stream_state_reader(primary, 1)(ray_id, 0);
        let ray   = make_ray_stream_reader(primary.rays, 1)(ray_id, 0);
        let pixel = primary.rays.id(ray_id);

        let hit   = make_primary_stream_hit_reader(primary, 1)(ray_id, 0);
        let geom  = scene.geometries(geom_id);
        let surf  = geom.surface_element(ray, hit);
        let mat   = geom.shader(ray, hit, surf);

        for once() {
            @@(path_tracer.on_hit)(ray, hit, &mut state, surf, mat, @ |color| -> ! {
                gpu_accumulate(atomics, film_pixels, pixel, color, spp);
                break()
            });
        }

        for once() {
            @@(path_tracer.on_shadow)(ray, hit, &mut state, surf, mat, @ |ray, color| -> ! {
                make_ray_stream_writer(secondary.rays, 1)(ray_id, 0, ray);
                secondary.color_r(ray_id) = color.r;
                secondary.color_g(ray_id) = color.g;
                secondary.color_b(ray_id) = color.b;
                secondary.rays.id(ray_id) = pixel;
                break()
            }, @ || -> ! {
                secondary.rays.id(ray_id) = -1;
                break()
            })
        }

        for once() {
            @@(path_tracer.on_bounce)(ray, hit, &mut state, surf, mat, @ |new_ray, new_state| -> ! {
                make_ray_stream_writer(primary.rays, 1)(ray_id, 0, new_ray);
                make_primary_stream_state_writer(primary, 1)(ray_id, 0, new_state);
                break()
            }, @ || -> ! {
                primary.rays.id(ray_id) = -1;
                break()
            })
        }
    }
}

fn @copy_primary_ray( primary: PrimaryStream
                    , other_primary: PrimaryStream
                    , src_id: i32
                    , dst_id: i32
                    , keep_hit: bool
                    ) -> () {
    other_primary.rays.id(dst_id)    = primary.rays.id(src_id);
    other_primary.rays.org_x(dst_id) = primary.rays.org_x(src_id);
    other_primary.rays.org_y(dst_id) = primary.rays.org_y(src_id);
    other_primary.rays.org_z(dst_id) = primary.rays.org_z(src_id);
    other_primary.rays.dir_x(dst_id) = primary.rays.dir_x(src_id);
    other_primary.rays.dir_y(dst_id) = primary.rays.dir_y(src_id);
    other_primary.rays.dir_z(dst_id) = primary.rays.dir_z(src_id);
    other_primary.rays.tmin(dst_id)  = primary.rays.tmin(src_id);
    other_primary.rays.tmax(dst_id)  = primary.rays.tmax(src_id);
    if keep_hit {
        other_primary.geom_id(dst_id) = primary.geom_id(src_id);
        other_primary.prim_id(dst_id) = primary.prim_id(src_id);
        other_primary.t(dst_id)       = primary.t(src_id);
        other_primary.u(dst_id)       = primary.u(src_id);
        other_primary.v(dst_id)       = primary.v(src_id);
    }
    other_primary.rnd(dst_id)        = primary.rnd(src_id);
    other_primary.mis(dst_id)        = primary.mis(src_id);
    other_primary.contrib_r(dst_id)  = primary.contrib_r(src_id);
    other_primary.contrib_g(dst_id)  = primary.contrib_g(src_id);
    other_primary.contrib_b(dst_id)  = primary.contrib_b(src_id);
    other_primary.depth(dst_id)      = primary.depth(src_id);
}

fn @gpu_sort_primary( primary: PrimaryStream
                    , other_primary: PrimaryStream
                    , dev_id: i32
                    , acc: Accelerator
                    , intrinsics: Intrinsics
                    , atomics: Atomics
                    , scene: Scene
                    , gpu_tmp:  &mut [i32]
                    , ray_ends: &mut [i32]
                    ) -> () {
    let block_w = 64;

    // Fill ray_ends with 0s
    with work_item in acc.exec((round_up(scene.num_geometries + 1, block_w), 1, 1), (block_w, 1, 1)) {
        let geom_id = work_item.gidx();
        if geom_id > scene.num_geometries {
            break()
        }
        gpu_tmp(geom_id) = 0;
    }
    acc.sync();

    // Count number of rays for each geometry
    with work_item in acc.exec((round_up(primary.size, block_w), 1, 1), (block_w, 1, 1)) {
        let ray_id = work_item.gidx();
        if ray_id >= primary.size {
            break()
        }

        atomics.add_global_i32(&mut gpu_tmp(primary.geom_id(ray_id)), 1);
    }
    acc.sync();

    // Perform a scan on the CPU (there are very few elements to scan)
    let mut ray_begins : [i32 * 1024];
    runtime_copy(dev_id, gpu_tmp as &[i8], 0i64, 0, ray_ends as &[i8], 0i64, (sizeof[i32]() * (scene.num_geometries + 1)) as i64);
    let mut n = 0;
    for i in range(0, scene.num_geometries + 1) {
        ray_begins(i) = n;
        n += ray_ends(i);
        ray_ends(i) = n;
    }
    runtime_copy(0, &ray_begins as &[i8], 0i64, dev_id, gpu_tmp as &[i8], 0i64, (sizeof[i32]() * (scene.num_geometries + 1)) as i64);

    // Sort rays
    with work_item in acc.exec((round_up(primary.size, block_w), 1, 1), (block_w, 1, 1)) {
        let src_id = work_item.gidx();
        if src_id >= primary.size {
            break()
        }
        let geom_id = primary.geom_id(src_id);
        let dst_id  = atomics.add_global_i32(&mut gpu_tmp(geom_id), 1);
        copy_primary_ray(primary, other_primary, src_id, dst_id, true);
    }
    acc.sync();
}

fn @gpu_generate_rays( primary: PrimaryStream
                     , capacity: i32
                     , acc: Accelerator
                     , intrinsics: Intrinsics
                     , path_tracer: PathTracer
                     , id: &mut i32
                     , film_width: i32
                     , film_height: i32
                     , spp: i32
                     ) -> i32 {
    let block_w  = 64;
    let first_ray_id = *id;
    let first_dst_id = primary.size;
    let num_rays = cpu_intrinsics.min(spp * film_width * film_height - first_ray_id, capacity - first_dst_id);
    let film_div = make_fast_div(film_width as u32);

    let ray_ids     = primary.rays.id;
    let write_ray   = make_ray_stream_writer(primary.rays, 1);
    let write_state = make_primary_stream_state_writer(primary, 1);
    let write_id    = @ |i, _, id| ray_ids(i) = id;

    with work_item in acc.exec((round_up(num_rays, block_w), 1, 1), (block_w, 1, 1)) {
        let gid = work_item.gidx();
        if gid > num_rays {
            break()
        }

        let ray_id = first_ray_id + gid;
        let dst_id = first_dst_id + gid;
        let sample = ray_id % spp;
        let pixel  = ray_id / spp;
        let y = fast_div(film_div, pixel as u32) as i32;
        let x = pixel - y * film_width;
        let (ray, state) = @@(path_tracer.on_emit)(sample, x, y, film_width, film_height);
        write_ray(dst_id, 0, ray);
        write_state(dst_id, 0, state);
        write_id(dst_id, 0, pixel);
    }
    acc.sync();

    *id += num_rays;
    primary.size + num_rays
}

fn @gpu_compact_primary( primary: PrimaryStream
                       , other_primary: PrimaryStream
                       , dev_id: i32
                       , acc: Accelerator
                       , intrinsics: Intrinsics
                       , atomics: Atomics
                       , gpu_tmp: &mut [i32]
                       ) -> i32 {
    let block_w = 64;

    // reset temporary memory
    let mut size = 0;
    runtime_copy(0, &size as &[i8], 0i64, dev_id, gpu_tmp as &[i8], 0i64, sizeof[i32]() as i64);

    // Compact primary rays into another queue
    with work_item in acc.exec((round_up(primary.size, block_w), 1, 1), (block_w, 1, 1)) {
        let src_id = work_item.gidx();
        if src_id >= primary.size {
            break()
        }

        let ray_id = primary.rays.id(src_id);
        if ray_id < 0 {
            break()
        }

        let dst_id = atomics.add_global_i32(&mut gpu_tmp(0), 1);
        copy_primary_ray(primary, other_primary, src_id, dst_id, false);
    }
    acc.sync();

    runtime_copy(dev_id, gpu_tmp as &[i8], 0i64, 0, &mut size as &[i8], 0i64, sizeof[i32]() as i64);
    size
}

fn @gpu_swap_streams(a: &mut PrimaryStream, b: &mut PrimaryStream) -> () {
    let tmp = *a;
    *a = *b;
    *b = tmp;
}

fn @gpu_streaming_trace( dev_id: i32
                       , acc: Accelerator
                       , intrinsics: Intrinsics
                       , atomics: Atomics
                       , min_max: MinMax
                       , scene: Scene
                       , path_tracer: PathTracer
                       , spp: i32
                       ) -> () {
    let (film_pixels, film_width, film_height) = gpu_get_film_data(dev_id);

    let capacity = 1024 * 1024;
    let mut primary;
    let mut other_primary;
    let mut secondary;
    rodent_gpu_get_first_primary_stream(dev_id, &mut primary, capacity);
    rodent_gpu_get_second_primary_stream(dev_id, &mut other_primary, capacity);
    rodent_gpu_get_secondary_stream(dev_id, &mut secondary, capacity);

    let mut gpu_tmp : &mut [i32];
    rodent_gpu_get_tmp_buffer(dev_id, &mut gpu_tmp, 1024);

    let mut id = 0;
    let num_rays = spp * film_width * film_height;
    while id < num_rays || primary.size > 0 {
        // Regenerate rays
        if primary.size < capacity && id < num_rays {
            primary.size = gpu_generate_rays(primary, capacity, acc, intrinsics, path_tracer, &mut id, film_width, film_height, spp);
        }

        // Traverse primary rays
        gpu_traverse_primary(primary, acc, intrinsics, min_max, scene);

        // Sort rays by geometry
        let mut ray_ends : [i32 * 1024];
        gpu_sort_primary(primary, other_primary, dev_id, acc, intrinsics, atomics, scene, gpu_tmp, &mut ray_ends);
        gpu_swap_streams(&mut primary, &mut other_primary);

        // Shade rays
        let mut first = 0;
        for geom_id in unroll(0, scene.num_geometries) {
            let last = ray_ends(geom_id);
            if first >= last {
                continue()
            }
            gpu_shade(acc, atomics, scene, path_tracer, film_pixels, spp, primary, secondary, first, last, geom_id);
            first = last;
        }
        primary.size   = first;
        secondary.size = first;
        acc.sync();

        if first > 0 {
            // Trace secondary rays
            gpu_traverse_secondary(secondary, acc, intrinsics, atomics, min_max, scene, film_pixels, spp);

            // Compact primary rays
            other_primary.size = gpu_compact_primary(primary, other_primary, dev_id, acc, intrinsics, atomics, gpu_tmp);
            gpu_swap_streams(&mut primary, &mut other_primary);
        }
    }
}

fn @gpu_mega_kernel_trace(dev_id: i32, acc: Accelerator, intrinsics: Intrinsics, atomics: Atomics, min_max: MinMax, scene: Scene, path_tracer: PathTracer, spp: i32) -> () {
    let (film_pixels, film_width, film_height) = gpu_get_film_data(dev_id);

    // Make tiles of 2^10 = 1024 total samples
    let log2_tile_size = 10 - ilog2(spp);
    let expected_tile_w = 1 << (log2_tile_size / 2);
    let expected_tile_h = 1 << (log2_tile_size / 2);

    let (bx, by, bz) = (16, 8, 1);
    let gx = bx * (film_width  + expected_tile_w - 1) / expected_tile_w;
    let gy = by * (film_height + expected_tile_h - 1) / expected_tile_h;
    let gz = 1;
    with work_item in acc.exec((gx, gy, gz), (bx, by, bz)) {
        let tile_x = work_item.bidx() * expected_tile_w;
        let tile_y = work_item.bidy() * expected_tile_h;
        let tile_w = intrinsics.min(film_width  - tile_x, expected_tile_w);
        let tile_h = intrinsics.min(film_height - tile_y, expected_tile_h);
        let tile_div = make_fast_div(tile_w as u32);
        let ray_count = tile_w * tile_h * spp;
        let counter = &mut reserve_shared[i32](1)(0);
        if work_item.tidx() == 0 {
            *counter = 0
        }
        // Ensure consistent view of the counter
        acc.barrier();

        let mut ray;
        let mut state;
        let mut final_color;
        let mut done = true;
        let mut pixel = -1;

        while true {
            if done {
                if pixel >= 0 {
                    atomics.add_global_f32(&mut film_pixels(pixel + 0), final_color.r * (1.0f / (spp as f32)));
                    atomics.add_global_f32(&mut film_pixels(pixel + 1), final_color.g * (1.0f / (spp as f32)));
                    atomics.add_global_f32(&mut film_pixels(pixel + 2), final_color.b * (1.0f / (spp as f32)));
                }

                let id = atomics.add_shared_i32(counter, 1);
                if id >= ray_count { break() }
                let ray_id    = id / spp;
                let sample_id = id % spp;
                let in_tile_y = fast_div(tile_div, ray_id as u32) as i32;
                let in_tile_x = ray_id - in_tile_y * tile_w;
                let x = tile_x + in_tile_x;
                let y = tile_y + in_tile_y;
                let (init_ray, init_state) = @@(path_tracer.on_emit)(sample_id, x, y, film_width, film_height);
                final_color = make_color(0.0f, 0.0f, 0.0f);
                pixel = 3 * (y * film_width + x);
                ray   = init_ray;
                state = init_state;
            }
            done = true;

            let hit = gpu_traverse_single_helper(intrinsics, min_max, ray, scene.bvh, false /*any_hit*/, 1 /*root*/);
            if hit.prim_id == -1 {
                continue()
            }

            let mut shadow_color;
            let mut shadow_ray;
            let mut trace_shadow = false;
            for i in unroll(0, scene.num_geometries) {
                if i == scene.num_geometries - 1 || hit.geom_id == i {
                    let geom = scene.geometries(i);
                    let surf = geom.surface_element(ray, hit);
                    let mat  = geom.shader(ray, hit, surf);

                    for once() {
                        @@(path_tracer.on_hit)(ray, hit, &mut state, surf, mat, @ |color| -> ! {
                            final_color = color_add(final_color, color);
                            break()
                        });
                    }

                    for once() {
                        @@(path_tracer.on_shadow)(ray, hit, &mut state, surf, mat, @ |ray, color| -> ! {
                            shadow_color = color;
                            shadow_ray   = ray;
                            trace_shadow = true;
                            break()
                        });
                    }

                    for once() {
                        @@(path_tracer.on_bounce)(ray, hit, &mut state, surf, mat, @ |new_ray, new_state| -> ! {
                            ray   = new_ray;
                            state = new_state;
                            done  = false;
                            break()
                        });
                    }
                    break()
                }
            }

            if trace_shadow && gpu_traverse_single_helper(intrinsics, min_max, shadow_ray, scene.bvh, true /*any_hit*/, 1 /*root*/).prim_id == -1 {
                final_color = color_add(final_color, shadow_color);
            }
        }
    }
    acc.sync()
}

// GPU device ----------------------------------------------------------------------

fn @make_gpu_device( dev_id: i32
                   , acc: Accelerator
                   , intrinsics: Intrinsics
                   , min_max: MinMax
                   , load_bvh: fn (&[u8]) -> Bvh
                   , read_pixel: fn (&[u32], i32) -> u32
                   , make_buffer: fn (&[i8]) -> DeviceBuffer
                   , atomics: Atomics
                   , streaming: bool
                   ) -> Device {
    Device {
        intrinsics: intrinsics,
        trace: @ |scene, path_tracer, spp| {
            if streaming {
                gpu_streaming_trace(
                    dev_id,
                    acc,
                    intrinsics,
                    atomics,
                    min_max,
                    scene,
                    path_tracer,
                    spp
                )
            } else {
                gpu_mega_kernel_trace(
                    dev_id,
                    acc,
                    intrinsics,
                    atomics,
                    min_max,
                    scene,
                    path_tracer,
                    spp
                )
            }
        },
        present: @ || rodent_present(dev_id),
        load_buffer: @ |filename| make_buffer(rodent_load_buffer(dev_id, filename)),
        load_bvh: load_bvh,
        load_png: @ |filename| {
            let mut pixel_data;
            let mut width;
            let mut height;
            rodent_load_png(dev_id, filename, &mut pixel_data, &mut width, &mut height);
            let (ptr, stride) = (pixel_data, width);
            make_image_rgba32(@ |x, y| read_pixel(ptr as &[u32], y * stride + x), width, height)
        },
        load_jpg: @ |filename| {
            let mut pixel_data;
            let mut width;
            let mut height;
            rodent_load_jpg(dev_id, filename, &mut pixel_data, &mut width, &mut height);
            let (ptr, stride) = (pixel_data, width);
            make_image_rgba32(@ |x, y| read_pixel(ptr as &[u32], y * stride + x), width, height)
        }
    }
}

fn @make_nvvm_device(dev: i32, streaming: bool) -> Device {
    let dev_id = runtime_device(1, dev);
    let load_bvh = @ |filename| {
        let mut nodes;
        let mut tris;
        rodent_load_bvh2_tri1(dev_id, filename, &mut nodes, &mut tris);
        make_gpu_bvh2_tri1(nodes, tris, true)
    };
    let read_pixel = @ |p, i| bitcast[u32](nvvm_ldg_i32(&p(i) as &[1]i32));
    let make_buffer = @ |p| {
        DeviceBuffer {
            load_f32:  @ |i| nvvm_ldg_f32(&((p as &[1][f32])(i))),
            load_i32:  @ |i| nvvm_ldg_i32(&((p as &[1][i32])(i))),
            load_vec2: @ |i| {
                let v = nvvm_ldg4_f32(&(p as &[1][simd[f32 * 4]](i)));
                make_vec2(v(0), v(1))
            },
            load_vec3: @ |i| {
                let v = nvvm_ldg4_f32(&((p as &[1][simd[f32 * 4]])(i)));
                make_vec3(v(0), v(1), v(2))
            },
            load_vec4: @ |i| {
                let v = nvvm_ldg4_f32(&(p as &[1][simd[f32 * 4]](i)));
                make_vec4(v(0), v(1), v(2), v(3))
            },
            load_int2: @ |i| {
                let v = nvvm_ldg4_i32(&(p as &[1][simd[i32 * 4]](i)));
                (v(0), v(1))
            },
            load_int3: @ |i| {
                let v = nvvm_ldg4_i32(&(p as &[1][simd[i32 * 4]](i)));
                (v(0), v(1), v(2))
            },
            load_int4: @ |i| {
                let v = nvvm_ldg4_i32(&(p as &[1][simd[i32 * 4]](i)));
                (v(0), v(1), v(2), v(3))
            }
        }
    };
    let atomics = Atomics {
        add_global_i32: @ |p, i| nvvm_atomic_add_global(p as &mut[1]i32, i),
        add_global_f32: @ |p, i| {
            let mut old: f32;
            asm("atom.global.add.f32 $0, [$1], $2;" : "=f"(old) : "l"(p), "f"(i) : "memory");
            old
        },
        add_shared_i32: @ |p, i| nvvm_atomic_add_shared(p, i),
        add_shared_f32: @ |p, i| {
            let mut old: f32;
            asm("atom.shared.add.f32 $0, [$1], $2;" : "=f"(old) : "l"(p), "f"(i) : "memory");
            old
        }
    };
    make_gpu_device(
        dev_id,
        nvvm_accelerator(dev),
        nvvm_intrinsics,
        make_nvvm_min_max(),
        load_bvh,
        read_pixel,
        make_buffer,
        atomics,
        streaming
    )
}

fn @make_amdgpu_device(dev: i32, streaming: bool) -> Device {
    let dev_id = runtime_device(3, dev);
    let load_bvh = @ |filename| {
        let mut nodes;
        let mut tris;
        rodent_load_bvh2_tri1(dev_id, filename, &mut nodes, &mut tris);
        make_gpu_bvh2_tri1(nodes, tris, false)
    };
    let read_pixel = @ |p, i| p(i);
    let make_buffer = @ |p| {
        DeviceBuffer {
            load_f32:  @ |i| (p as &[1][f32])(i),
            load_i32:  @ |i| (p as &[1][i32])(i),
            load_vec2: @ |i| {
                let v = (p as &[1][simd[f32 * 4]])(i);
                make_vec2(v(0), v(1))
            },
            load_vec3: @ |i| {
                let v = (p as &[1][simd[f32 * 4]])(i);
                make_vec3(v(0), v(1), v(2))
            },
            load_vec4: @ |i| {
                let v = (p as &[1][simd[f32 * 4]])(i);
                make_vec4(v(0), v(1), v(2), v(3))
            },
            load_int2: @ |i| {
                let v = (p as &[1][simd[i32 * 4]])(i);
                (v(0), v(1))
            },
            load_int3: @ |i| {
                let v = (p as &[1][simd[i32 * 4]])(i);
                (v(0), v(1), v(2))
            },
            load_int4: @ |i| {
                let v = (p as &[1][simd[i32 * 4]])(i);
                (v(0), v(1), v(2), v(3))
            }
        }
    };
    let atomics = Atomics {
        add_global_i32: @ |p, i| amdgcn_atomic_add_global(p as &mut[1]i32, i),
        add_global_f32: @ |p, i| {
            let mut assumed = *p;
            while true {
                let (old, success) = amdgcn_atomic_cmpxchg_global(p as &mut [1]i32, bitcast[i32](assumed), bitcast[i32](i + assumed));
                assumed = bitcast[f32](old);
                if success { break() }
            }
            assumed
        },
        add_shared_i32: @ |p, i| amdgcn_atomic_add_shared(p, i),
        add_shared_f32: @ |p, i| {
            let mut assumed = *p;
            while true {
                let (old, success) = amdgcn_atomic_cmpxchg_shared(p as &mut [3]i32, bitcast[i32](assumed), bitcast[i32](i + assumed));
                assumed = bitcast[f32](old);
                if success { break() }
            }
            assumed
        }
    };
    make_gpu_device(
        dev_id,
        amdgpu_accelerator(dev),
        amdgpu_intrinsics,
        make_amdgpu_min_max(),
        load_bvh,
        read_pixel,
        make_buffer,
        atomics,
        streaming
    )
}
